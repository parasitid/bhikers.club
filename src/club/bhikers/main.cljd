(ns club.bhikers.main
  "Bhikers Club App by ParaSitid"
  (:require ["package:flutter/material.dart" :as m]
            ["package:easy_localization/easy_localization.dart" :as l10n]
            ["package:easy_localization_loader/easy_localization_loader.dart" :as l10nassets]
            ["package:flutter_settings_screens/flutter_settings_screens.dart" :as settings]
            [club.bhikers.screens.aroundme :refer [around-me-screen]]
            [club.bhikers.screens.settings :refer [settings-screen]]
            [club.bhikers.screens.enhancegpx :refer [enhance-gpx-screen]]
            [club.bhikers.screens.falldetector :refer [fall-detector-screen]]
            [club.bhikers.lib.app :refer [supported-locales default-locale]]
            [club.bhikers.lib.pois :refer [new-overpass-api]]
            [club.bhikers.lib.utils :refer [log create-cache-store]]
            [club.bhikers.lib.notifications :refer [debug-notif]]
            [cljd.flutter :as f]))

;; Around me ( +/- 10/20km): POIs*
;; Enhance GPX: upload gpx and add POIs*
;; Fall Detector Alert: use accelerometer to detect fall or no movement
;; Settings: avatar, sms/signal/telegram accounts to send alerts
;;
;; *POIs: bhikers, bakeries+resto, hotels+campings+spots, foutains for bottle refills

;; Setup default values


;; Setup Theme of the material app
(def theme
  (let [color-scheme (m/ColorScheme.fromSeed .seedColor m/Colors.blue
                                             .brightness m/Brightness.light
                                             .contrastLevel -0.5)]
    (m/ThemeData
     .colorScheme color-scheme
     .floatingActionButtonTheme (m/FloatingActionButtonThemeData
                                 .backgroundColor (.-tertiary color-scheme)
                                 .foregroundColor (.-onTertiary color-scheme))
     .useMaterial3 true
     .textTheme (m/TextTheme
                 .displayLarge (m/TextStyle
                                .fontWeight m/FontWeight.w700
                                .fontSize 24
                                .color m/Colors.black)))))

;; Main:
;; init settings, localizqtion
;; run MaterialApp with default screen "Around Me"
(defn main []
  ;; careful: order matters
  (log "init settings with shared preference cache provider")
  (settings/Settings.init .cacheProvider (settings/SharePreferenceCache))
  (log "init flutter bindings")
  (m/WidgetsFlutterBinding.ensureInitialized)
  (log "init easy l10n")
  (await (l10n/EasyLocalization.ensureInitialized))
  (debug-notif "starting app")
  (f/run
    (f/widget
     (l10n/EasyLocalization
      .supportedLocales (map #(m/Locale %) supported-locales)
      .fallbackLocale (m/Locale default-locale)
      .saveLocale true
      .useOnlyLangCode true
      .useFallbackTranslations true
      .assetLoader (l10nassets/YamlAssetLoader)
      .path "src/resources/langs")
     (f/widget
      :context ctx
      :managed [cache-store (create-cache-store 20 (* 1024 1024)) :dispose .close
                overpass-api (new-overpass-api cache-store)
                :dispose .close]
      :bind {:overpass-api overpass-api}
      (m/MaterialApp
       .localizationsDelegates (-> ctx l10n/BuildContextEasyLocalizationExtension .-localizationDelegates)
       .supportedLocales (-> ctx l10n/BuildContextEasyLocalizationExtension .-supportedLocales)
       .locale (-> ctx l10n/BuildContextEasyLocalizationExtension .-locale)
       .initialRoute "/around-me"
       .theme theme
       .routes (into {}
                     (keep (fn [[k v]] {k (fn ^m/Widget k [_] (v))}))
                     {"/around-me" around-me-screen
                      "/enhance-gpx" enhance-gpx-screen
                      "/fall-detector" fall-detector-screen
                      "/settings" settings-screen}))))))
